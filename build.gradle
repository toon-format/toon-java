plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
}

group = 'dev.toonformat'
version = providers.gradleProperty('version').getOrElse('1.0.5')
description = 'Token-Oriented Object Notation (TOON) - A compact, human-readable format for LLM contexts'

// Apply external Gradle scripts
apply from: 'gradle/publishing.gradle'
apply from: 'gradle/verification.gradle'

java {
    // Only enforce toolchain in CI to ensure consistent Java 17 builds
    // Locally, use whatever JDK is installed
    if (System.getenv('CI') == 'true') {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }
    withSourcesJar()
    withJavadocJar()
}

repositories {
    mavenCentral()
}

jacoco {
    toolVersion = "0.8.14"
    reportsDirectory = layout.buildDirectory.dir('customJacocoReportDir')
}

dependencies {
    implementation 'tools.jackson.core:jackson-databind:3.0.3'
    implementation 'tools.jackson.module:jackson-module-afterburner:3.0.3'
    testImplementation platform('org.junit:junit-bom:6.0.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test
    reports {
        csv.required = true
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = "BUNDLE"
            limit {
                counter = "LINE"
                value = "COVEREDRATIO"
                minimum = "0.90".toBigDecimal()
            }

            limit {
                counter = "BRANCH"
                value = "COVEREDRATIO"
                minimum = "0.80".toBigDecimal()
            }
        }
    }
}
check.dependsOn jacocoTestReport

tasks.register('generateJavadoc', Javadoc) {
    description = 'Generates Javadoc HTML documentation in the docs/javadoc folder'
    group = 'docs'
    
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
    destinationDir = file('docs/javadoc')
    
    options.encoding = 'UTF-8'
    options.docEncoding = 'UTF-8'
    options.charSet = 'UTF-8'
}

tasks.register('specsValidation', Test) {
    description = 'Run the schema validation test'
    group = 'verification'

    include '**/ConformanceTest.class'
}
